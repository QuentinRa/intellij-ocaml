plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.4.0'
    id 'org.jetbrains.grammarkit' version '2021.2.1'
    id 'net.saliman.properties' version '1.5.2'
    id 'org.gradle.test-retry' version '1.3.1'
    id 'com.adarshr.test-logger' version '3.2.0'
}

testlogger {
    theme 'mocha'
    showStandardStreams true
    showSkipped false
}

// The same as `--stacktrace` param
gradle.startParameter.showStacktrace = ShowStacktrace.ALWAYS

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'org.jetbrains.grammarkit'
apply plugin: 'org.gradle.test-retry'

repositories {
    mavenCentral()
}

configurations {
    all {
        // Allows using project dependencies instead of IDE dependencies
        // during compilation and test running
        resolutionStrategy.sortArtifacts(ResolutionStrategy.SortOrder.DEPENDENCY_FIRST)
    }
}

group = 'com.ocaml'
version = "$majorVersion.$minorVersion.$patchVersion-$platformVersion" as String

intellij {
    version = ideaVersion
    pluginName = "intellij-ocaml"
    plugins = [
            'java',
            'com.jetbrains.hackathon.indices.viewer:'+indicesVersion,
            'PsiViewer:'+psiViewerPluginVersion
    ]
    downloadSources = true
    sandboxDir = "$buildDir/idea-sandbox-$platformVersion"

    patchPluginXml {
        sinceBuild = pluginSinceBuild
        untilBuild = pluginUntilBuild
        changeNotes = """
                <ul>
                    <li>Remade of the auto-selection of a statement (#85)</li>
                    <li>Continuous integration (#83, internal)</li>
                    <li>Wider range of OCaml versions (#81)</li>
                    <li>Fixed sending statement with "and" to the REPL (#80)</li>
                    <li>Can send the selected code again! (#80)</li>
                    <li>Fix issue with multiline comments in the REPL (#80)</li>
                    <li>Can be used outside OCaml Modules (#75)</li>
                    <li>Executables are named {name}.out, fixing the issue (#71)</li>
                    <li>Fix empty variable view for type option (#63)</li>
                    <li>See <a href="https://github.com/QuentinRa/intellij-ocaml/blob/main/CHANGELOG.md">CHANGELOG.md</a></li>
                <ul>
            """.stripIndent()
    }
}

// https://plugins.jetbrains.com/plugin/6606-grammar-kit/versions
// 203.5784+
grammarKit {
    grammarKitRelease = '2021.1.2'
}

sourceCompatibility = '11.0'
compileJava.options.encoding = 'UTF-8'

// We are defining that the src/resources folders
// which are both a "main" folder inside resources/src/tests
// and a folder such as "212" or "213"
sourceSets {
    main.java.srcDirs = ["src/$platformVersion", "src/main"]
    if (loadPlatform != null && !loadPlatform.isBlank()) {
        for(String s: (loadPlatform as String).split(",")) {
            main.java.srcDirs += ["src/$s"]
        }
    }
    main.resources.srcDirs = ["resources/main", "resources/$platformVersion"]
    test.java.srcDirs = ["test/$platformVersion/", "test/main"]
    if (loadPlatform != null && !loadPlatform.isBlank()) {
        for(String s: (loadPlatform as String).split(",")) {
            test.java.srcDirs += ["test/$s"]
        }
    }
    test.resources.srcDirs = ["resources/main", "resources/$platformVersion", "test/testData"]
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:deprecation']
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

buildPlugin {
    // Set proper name for final plugin zip.
    // Otherwise, base name is the same as gradle module name
    archiveBaseName.set("intellij-ocaml")
}

runIde {
    systemProperty 'idea.is.internal', true

    // Default args for IDEA installation
    jvmArgs("-Xmx768m", "-XX:+UseConcMarkSweepGC", "-XX:SoftRefLRUPolicyMSPerMB=50")
    // Disable plugin auto reloading. See `com.intellij.ide.plugins.DynamicPluginVfsListener`
    jvmArgs("-Didea.auto.reload.plugins=false")
    // Don't show "Tip of the Day" at startup
    jvmArgs("-Dide.show.tips.on.startup.default.value=false")

    // Uncomment to enable localization testing mode
    // jvmArgs("-Didea.l10n=true")
}

// https://plugins.jetbrains.com/docs/intellij/api-changes-list.html
runPluginVerifier {
    ideVersions = [ideaVersion]
}

test {
    jvmArgs = ["-Xmx2g", "-XX:-OmitStackTraceInFastThrow"]
    // needed for newer versions
    if (JavaVersion.current().ordinal() >= JavaVersion.VERSION_14.ordinal()) {
        jvmArgs += [
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
                '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
                '--add-opens', 'java.base/java.io=ALL-UNNAMED',
                '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED',
                '--add-opens', 'jdk.unsupported/sun.misc=ALL-UNNAMED',
                '--add-opens', 'java.desktop/sun.awt=ALL-UNNAMED',
                '--add-opens', 'java.desktop/sun.font=ALL-UNNAMED',
                '--add-opens', 'java.desktop/sun.swing=ALL-UNNAMED',
                '--add-opens', 'java.desktop/sun.java2d=ALL-UNNAMED',
                '--add-opens', 'java.desktop/java.awt=ALL-UNNAMED',
                '--add-opens', 'java.desktop/java.awt.event=ALL-UNNAMED',
                '--add-opens', 'java.desktop/javax.swing=ALL-UNNAMED',
                '--add-opens', 'java.desktop/javax.swing.plaf.basic=ALL-UNNAMED',
        ]
    }
    // We need to prevent the platform-specific shared JNA library to loading from the system library paths,
    // because otherwise it can lead to compatibility issues.
    // Also note that IDEA does the same thing at startup, and not only for tests.
    systemProperty("jna.nosys", "true")
    filter.failOnNoMatchingTests(false)

    System.setProperty("idea.log.debug.categories", "ocaml.tests")
    System.setProperty("log4j.debug", "true")
    systemProperty "idea.log.debug.categories", "ocaml.tests"
    systemProperty "log4j.debug", true

    if (System.getenv("CI") != null) {
        retry {
            maxRetries.set(3)
            maxFailures.set(5)
        }
    }
}