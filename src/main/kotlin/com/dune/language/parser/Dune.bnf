{
  parserClass="com.dune.language.parser.DuneParser"

  implements='com.dune.language.psi.api.DuneElement'
  extends='com.dune.language.psi.api.DuneElementImpl'

  psiClassPrefix="Dune"
  psiImplClassSuffix="Impl"
  psiPackage="com.dune.language.psi"
  psiImplPackage="com.dune.language.psi.impl"

  elementTypeHolderClass="com.dune.language.psi.DuneTypes"
  elementTypeClass="com.dune.language.psi.api.DuneElementType"
  tokenTypeClass="com.dune.language.psi.api.DuneTokenType"

  generateTokenAccessors=true

  tokens = [
    LPAREN = "("
    RPAREN = ")"
    COMMENT = "COMMENT"
    WHITE_SPACE = "WHITE_SPACE"
  ]
}

// https://dune.readthedocs.io/en/stable/reference/lexical-conventions.html
// Since whitespace are separators, they are not considered as blank whitespace anymore
implementation ::= { items }*

private items ::= list | COMMENT | WHITE_SPACE

// Lists are sequences of values enclosed by parentheses.
// Lists can be empty, for instance: ().
list ::= LPAREN [WHITE_SPACE] [value { WHITE_SPACE argument }*] [WHITE_SPACE] RPAREN
{
    pin=1
    implements="com.dune.language.psi.api.DuneNamedOwnerElement"
    extends="com.dune.language.psi.ext.DuneNamedOwnerElementImpl"
}

value ::= atom | STRING_VALUE | list
argument ::= atom | STRING_VALUE | list { implements="value" }

// While both "a" and "a/b" are atoms
// Only "a" can be renamed or be a filename
// while "a/b", "a.{ml,mli}" "-a" may be renamed or not
// (not handled tho)
atom ::= named_atom | expr_atom

named_atom ::= ATOM_VALUE_ID
{
    implements="com.dune.language.psi.api.DuneNamedElement"
    extends="com.dune.language.psi.ext.DuneNamedElementImpl"
}

expr_atom ::= ATOM_VALUE_COMPLEX